(def car first)
(def cdr rest)
(def cadr (λ [x] (car (cdr x))))
(def cddr (λ [x] (cdr (cdr x))))
(def id (λ [x] x))

(defmacro ƒ
  [name args & body]
  `(def ~name (λ ~args ~@body)))

(defmacro unless
  [pred a b]
  `(if ~pred ~b ~a))

(ƒ not
  [a]
  (if a false true))

(ƒ load-file
  [f]
  (eval (read-string (str "(do " (slurp f) ")"))))

(def *gensym-counter* (atom 0))
(ƒ gensym []
  (symbol (str "G__" (swap! *gensym-counter* (λ [x] (+ 1 x))))))

(defmacro cond
  [& xs]
  `(if (> ~(count xs) 0)
    (if (or (= '~(first xs) 'else) ~(first xs))
       (if (> ~(count xs) 1)
         ~(nth xs 1)
         (error "odd number of forms to cond"))
    (cond ~@(rest (rest xs))))))

(ƒ let-fn
  [bindings body]
  (cond
    (= (count bindings) 2)
    `((λ (~(car bindings))
        ~@body)
      ~(cadr bindings))
    else
    `((λ (~(car bindings))
        ~(let-fn (cddr bindings) body))
      ~(cadr bindings))))

(defmacro let
  [bindings & body]
  (let-fn bindings body))

(defmacro and
  [x & rest]
  (cond
    (empty? rest) x
    else `(let [v ~x]
            (cond
              v (and ~@rest)
              else v))))

(defmacro or
  [& xs]
  (if (empty? xs)
    nil
    (if (= 1 (count xs))
      (first xs)
      (let* [condvar (gensym)]
        `(let* [~condvar ~(first xs)]
           (if ~condvar
             ~condvar
             (or ~@(rest xs))))))))

(defmacro is [form]
  `(cond (not ~form) (print (str "Assertion failed:" (quote ~form)))))

(defmacro is= [& body]
  `(cond (not (= ~@body)) (print (str "Equal Assertion failed"))))

(ƒ even?
  [n]
  (cond
    (and (int? n) (= 0 (% n 2))) true
    else false))

(ƒ odd?
  [n]
  (not (even? n)))

(ƒ map
  [f l]
  (foldr (λ [e acc]
           (cons (f e) acc))
         '() l))

(ƒ filter
  [f l]
  (foldr (λ [e acc]
           (cond
             (f e) (cons e acc)
             else acc))
         '() l))

;; cause there is no multi definition dispatch, so put the default acc as the last param
(ƒ reduce
  [f l & acc]
  (cond
    (empty? acc) (foldl f (f (car l) (cadr l)) (cddr l))
    else (foldl f (car acc) l)))

(ƒ append
  [& lists]
  (reduce (λ [lst1 lst2] (foldr (λ [head tail] (cons head tail)) lst2 lst1)) lists))

(ƒ reverse
  [l]
  (foldl (λ [acc e] (cons e acc)) nil l))

(defmacro ->
  [x & xs]
  (if (empty? xs)
    x
    (let [form (first xs)
          more (rest xs)]
      (if (empty? more)
        (if (list? form)
          ;; as the first argument
          `(~(first form) ~x ~@(rest form))
          `(~form ~x))
        `(-> (-> ~x ~form) ~@more)))))

(defmacro ->>
  [x & xs]
  (if (empty? xs)
    x
    (let [form (first xs)
          more (rest xs)]
      (if (empty? more)
        (if (list? form)
          ;; as the last argument
          `(~(first form) ~@(rest form) ~x)
          `(~form ~x))
        `(->> (->> ~x ~form) ~@more)))))
